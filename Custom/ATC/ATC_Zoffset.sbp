'ATC Offset calculator v1.080405
'    Gordon Bergfors, ShopBot Tools, Inc.
'     All Rights Reserved, Copyright 2002-2005
'	Speed changes on touch off lines 50 and 54 BHM 20Oct06  
'
'Determine whether we are in Preview mode or Move/Cut mode
   &modenow = %(22)
   IF &modenow = 1 THEN GOSUB Changemode

    SA,     
    SW, 0         'Deactivate Warning duration
    SF, 0         'Deactivate software limits
'    SC, 0         'Run in single step mode

    SO,1,0        'Make sure spindle is not running

'Initialize
    &th_z = 0     'Initialize to zero
    C#,90         'Load the custom variables file from Custom Cut 90
    C#,89         'Read variables from ATC_Variables and ATC_Tool_Variables
    &start_XYmove_Speed = %(71)
    &start_XYjog_Speed = %(76)
    &start_Zmove_speed = %(73)
    &start_Zjog_speed = %(78)

    &XYapproach_Speed = &start_XYmove_Speed
    &XYtempJog_Speed = &start_XYjog_Speed
    &Zapproach_Speed = &start_Zmove_Speed
    &ZtempJog_Speed = &start_Zjog_Speed

    If &XYapproach_Speed > &TopXY_ApproachSpeed Then &XYapproach_Speed = &TopXY_ApproachSpeed
    If &XYtempJog_Speed > &TopXY_JogSpeed Then &XYtempJog_Speed = &TopXY_JogSpeed
    If &Zapproach_Speed > &TopZ_ApproachSpeed Then &Zapproach_Speed = &TopZ_ApproachSpeed
    If &ZtempJog_Speed > &TopZ_JogSpeed Then &ZtempJog_Speed = &TopZ_JogSpeed
'Check status of input #3
    &prox_status = %(53)
    IF &prox_status = 0 THEN GOSUB Getoffprox

' set the location to move to before second contact
   
    VA,,,%(3) + %(8),,,,,0             'Return to Table Base Coordinate system

    IF &ToolIN = 1 THEN &th_z = &Tool1_Z
    IF &ToolIN = 2 THEN &th_z = &Tool2_Z
    IF &ToolIN = 3 THEN &th_z = &Tool3_Z
    IF &ToolIN = 4 THEN &th_z = &Tool4_Z
    IF &ToolIN = 5 THEN &th_z = &Tool5_Z
    IF &ToolIN = 6 THEN &th_z = &Tool6_Z
    IF &ToolIN = 7 THEN &th_z = &Tool7_Z
    IF &ToolIN = 8 THEN &th_z = &Tool8_Z
	IF &ToolIN = 9 THEN &th_z = &Tool9_Z
	IF &ToolIN = 10 THEN &th_z = &Tool10_Z
	IF &ToolIN = 11 THEN &th_z = &Tool11_Z
	IF &ToolIN = 12 THEN &th_z = &Tool12_Z
	IF &ToolIN = 13 THEN &th_z = &Tool13_Z
	IF &ToolIN = 14 THEN &th_z = &Tool14_Z
	IF &ToolIN = 15 THEN &th_z = &Tool15_Z
	IF &ToolIN = 16 THEN &th_z = &Tool16_Z
	IF &ToolIN = 17 THEN &th_z = &Tool17_Z
	IF &ToolIN = 18 THEN &th_z = &Tool18_Z
	IF &ToolIN = 19 THEN &th_z = &Tool19_Z
	IF &ToolIN = 20 THEN &th_z = &Tool20_Z
	IF &ToolIN = 21 THEN &th_z = &Tool21_Z
	IF &ToolIN = 22 THEN &th_z = &Tool22_Z
	IF &ToolIN = 23 THEN &th_z = &Tool23_Z
	IF &ToolIN = 24 THEN &th_z = &Tool24_Z

    VA,,,%(3)-&th_z,,,,,&th_z

'Place ground clip on bit and zero plate under - press ENTER to continue
    PAUSE

'Set the contact switch action ... Plate NOW SET FOR Input Switch #1
    ON INP(1,1) GOSUB CONTACT1
      JS,,&TopZ_JogSpeed  		 'Change jog speed to .75

	JZ, -&zSearch

    ON INP(1,1) GOSUB CONTACT2 'this is where we'll go on contact with plate

      MS,,&TopZ_JogSpeed	    	 'Change move speed to .75

	MZ, -&zSearch

'We're done with all moves so reset speeds
    VS,&start_XYmove_Speed,&start_Zmove_speed,,,&start_XYjog_Speed,&start_Zjog_speed         '... and assign slower speeds

    &zOffset = %(3) - &my_ZzeroThickness

    VA,,,,,,,,0             'Return to Table Base Coordinate system

    JZ, %(3)+ &zBackoff 'Jog to &ZUP to clear plate
  
    FP, &ATC_Writer

    VA,,,%(3)-&zOffset,,,,,&th_z+&zOffset  'Set Z height of bit relative to table surface from Table Base Coordinates

END            'This END statement causes the program to end here without
                '  dropping through to subroutine

'================================================================Subroutines
CONTACT1:
'This is our subroutine for action on hitting the plate the first time
  'We hit the plate ! 
  ON INP(1,0)   'First set switch to nothing to prevent secondary trigger
  PAUSE .2
  JZ, %(3)+&zBackoff  'Pull-down SLIGHTLY ... 
  'Now We're Ready to Make Slow Move for Accurate Reading
  PAUSE .2
 RETURN

CONTACT2:
'This is our subroutine for action on hitting the plate the first time
  'We hit the plate ! 
  ON INP(1,0)   'First set switch to nothing to prevent secondary trigger
  PAUSE .2
 RETURN         'Technically this RETURNs us to the next file line after the
                '   move that was interrupted by the switch ... eg. the MZ
                '   and we will then encounter the END and leave the file.

CONTACTZ:
  ON INP(1,0)
  JZ,%(3)-&zBackoff
 RETURN

Getoffprox:
   'Use Arrow Keys to move Off of Prox Switch (ESC when Clear)
    PAUSE
    SK    
 RETURN
