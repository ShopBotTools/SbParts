&xtrycount = 0
&ytrycount = 0
&numtries = 2
&xcontacts = 0
&ycontacts = 0
&xval = 0
&yval = 0
&movespeed = 1
&jogspeed = 2.5
IF %(25) = 1 THEN &movespeed = 25.4
IF %(25) = 1 THEN &jogspeed = 63.5

'Determine whether we are in Preview mode or Move/Cut mode
   &modenow = %(22)
   IF &modenow = 1 THEN GOSUB Changemode
' check if at zero point
 MSGBOX(Do you want to use the Keypad to move to your 0 0 position? ,YesNo,Move To 0 0)
 If &msganswer = YES Then GOSUB SHOWKEYPAD
 MSGBOX(Zero your X and Y axis here? ,YesNo,Zero Here)
 If &msganswer = NO Then GOTO DONE
   Z2
'----------------------------------------------------------------Read the starting speed values to reset later  
   &start_XYmove_Speed = %(71)
   &start_XYjog_Speed = %(76)
   &XYapproach_Speed = &start_XYmove_Speed
   &XYtempJog_Speed = &start_XYjog_Speed
   If &XYapproach_Speed > &movespeed then &XYapproach_Speed = &movespeed
   If &XYtempJog_Speed > &jogspeed then &XYtempJog_Speed = &jogspeed
   VS, &XYapproach_Speed,,,,&XYtempJog_Speed                    '... and assign slower speeds
'---------------------------------------------------------------------------------------------------------------
                            'This version of XYzero deals with only two possibilities ...
   &SW2_SwitchType = %(92)  '  If #2 Input is 2, then we have (PRS) normally-closed switches on #2 and #3                  
                            '  Otherwise, we have a (PRT) with normally-open switches on #3
Check_Switch_Status:
'Check status of current inputs to know if we have to clear switches
   IF &SW2_SwitchType = 2 THEN GOTO Check_PRS
  Check_PRT: 
    &TrgtHIT = 1
    &Xtrgt = 3
    &Ytrgt = 3
    IF %(53) = 1 THEN GOTO Getoffprox  
    GOTO Start_Zeroing
  Check_PRS: 
    &TrgtHIT = 0
    &Xtrgt = 2
    &Ytrgt = 3
    IF %(53) = 0 THEN GOTO Getoffprox  
    IF %(52) = 0 THEN GOTO Getoffprox
Start_Zeroing:
'Initialize stuff
   SA      'Set tool to Absolute mode
   SC, 0        'In general, it is a good idea to turn off stack creation in a
       '  switch-oriented program like this to prevent unexpected
       '  effects related to the pre-processing of all movement
       '  commands. Alternatively SC,2 could be used to break stack
       '  activity into chunks.
'Limits are deactivated here, automatically reset to original state when file ends 
   VN, 0                    'Deactivate the limit switch functionality on input #3
   SF, 0                    'Deactivate software limits
'Start Homing in the X axis
  'First set the contact switch action ... 
REDO_X:
   ON INP(&Xtrgt,&TrgtHIT) GOSUB Xcontact   'this is where we'll go on contact with switch
  'Then start the move in X (just a big negative number ... assumes we'll hit prox switch)
  MX, -500
  IF &xtrycount < &numtries then GOTO REDO_X
REDO_Y:
   ON INP(&Ytrgt,&TrgtHIT) GOSUB YCONTACT 'this is where we'll go on contact with switch
   MY, -500 
   IF &ytrycount < &numtries then GOTO REDO_Y
  ' We return here after handling X homing
  ' ... and reset speeds
   VS, &start_XYmove_Speed,,,,&start_XYjog_Speed
&xval = &xcontacts / &numtries
&yval = &ycontacts / &numtries 
&setup_units = %(25)
&outname =C:\SbParts\Custom\xy_proxvals.tmp
   OPEN &outname FOR OUTPUT AS #1
   WRITE #1, &xval
   WRITE #1, &yval
   WRITE #1, &setup_units
close #1
'exit shopbot
end
'SUBROUTINES ---------------------------------------------------------------------------
Xcontact:
 'This is our subroutine for action on hitting the switch
   'We hit the limit switch! 
       ON INP(&Xtrgt,&TrgtHIT)  'Set switch to nothing to prevent secondary trigger
       &xcontacts = &xcontacts + %(1)
       &xtrycount = &xtrycount + 1
       JX, 0        'Move back out for another pass
   PAUSE 1
 RETURN           'This RETURNs us to the next main file line
Ycontact:
 'This is our subroutine for action on hitting the switch
   'We hit the limit switch! 
     ON INP(&Ytrgt,&TrgtHIT)  'Set switch to nothing to prevent secondary trigger
     &ycontacts = &ycontacts + %(2)
     &ytrycount = &ytrycount + 1
     JY, 0        'Move back out for another pass
     PAUSE 1
 RETURN           'This RETURNs us to the next main file line
            'interrupted by the switch
Changemode:
   'Tool won't home in Preview Mode. Quit and change to Move/Cut mode ...
    PAUSE 
 END              'Exit program now
Getoffprox:
   'One of the prox switches is turned on already. Check the connections and target setup and then try again
    PAUSE
    EXIT    
 RETURN
SHOWKEYPAD:
     SK 
RETURN
DONE:
   END
RETURN

